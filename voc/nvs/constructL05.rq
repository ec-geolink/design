## This query is issued to NERC vocabulary server's SPARQL endpoint at http://vocab.nerc.ac.uk/sparql/
## Then, the result is saved into XML format as L05.owl

PREFIX owl: <http://www.w3.org/2002/07/owl#>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX xml: <http://www.w3.org/XML/1998/namespace>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX dcterms: <http://purl.org/dc/terms/>
## PREFIX foaf: <http://xmlns.com/foaf/0.1/>
## PREFIX geosparql: <http://www.opengis.net/ont/geosparql#>
## PREFIX time: <http://www.w3.org/2006/time#>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX glvoc_L05: <http://schema.geolink.org/dev/voc/nvs/L05#>
PREFIX glvoc_L22: <http://schema.geolink.org/dev/voc/nvs/L22#>
PREFIX glview: <http://schema.geolink.org/dev/view#>

CONSTRUCT {
	?L05destURI a owl:Ontology ;
		rdfs:label ?L05graphdestLabel ;
		dcterms:date ?L05graphdestDate ;
		owl:versionInfo ?L05graphdestVers ;
		rdfs:comment "This ontology captures SeaDataNet device categories from NERC vocabulary server"@en ;
		rdfs:seeAlso ?L05sourceURI .
	
	glview:Instrument a owl:Class .
	glview:InstrumentType a owl:Class .
	glview:hasInstrumentType a owl:ObjectProperty .
	
	?L05destTermURI a owl:Class, owl:NamedIndividual, glview:InstrumentType ; 
		rdfs:label ?L05destTermLabel ;
		rdfs:comment ?L05destTermDef ;
		rdfs:seeAlso ?L05destTermSeeAlso ;
		rdfs:subClassOf ?L05destSuperTermURI.
	[] 	a owl:Restriction; 
		owl:onProperty glview:hasInstrumentType; 
		owl:someValuesFrom [ a owl:Class ; owl:oneOf ( ?L05destTermURI ) ] ;
		rdfs:subClassOf ?L05destTermURI .	
		
}
WHERE {
	SELECT 	DISTINCT *
	WHERE {
		VALUES ( ?L05sourceURI ?L05destURI ) { ( <http://vocab.nerc.ac.uk/collection/L05/current/> <http://schema.geolink.org/dev/voc/nvs/L05> ) }
		?L05sourceURI skos:member ?L05sourceTermURI ; 
			skos:prefLabel ?L05graphdestLabel ; 
			dcterms:date ?L05graphdestDate ; 
			owl:versionInfo ?L05graphdestVers .	
		
		?L05sourceTermURI skos:prefLabel ?L05sourceTermLabel;  skos:definition ?L05sourceTermDef .
		{	?L05sourceURI skos:member ?L05sourceSuperTermURI.
			?L05sourceTermURI skos:broader ?L05sourceSuperTermURI .		
		} UNION {		
			FILTER NOT EXISTS { ?L05sourceURI skos:member ?L05sourceSuperTermURI . ?L05sourceTermURI skos:broader ?L05sourceSuperTermURI .}	
		}
		BIND ( ?L05sourceTermLabel as ?L05destTermLabel )
		BIND ( ?L05sourceTermDef as ?L05destTermDef )
		BIND ( ?L05sourceTermURI as ?L05destTermSeeAlso )
		BIND ( replace( replace( str(?L05sourceTermURI), str(?L05sourceURI), "", "i"), "/", "", "i") as ?L05term )
		BIND ( iri(concat(str(?L05destURI), "#", ?L05term )) as ?L05destTermURI )					
		BIND ( 
			if( !bound(?L05sourceSuperTermURI), 
				glview:Instrument, 
				iri(concat(str(?L05destURI), "#", replace(replace(str(?L05sourceSuperTermURI), str(?L05sourceURI), "", "i"), "/", "", "i") )) ) 
			as ?L05destSuperTermURI )
	
	}
}
