## This query is issued to NERC vocabulary server's SPARQL endpoint at http://vocab.nerc.ac.uk/sparql/
## Then, the result is saved into XML format as P03.owl

PREFIX owl: <http://www.w3.org/2002/07/owl#>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX xml: <http://www.w3.org/XML/1998/namespace>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX dcterms: <http://purl.org/dc/terms/>
## PREFIX foaf: <http://xmlns.com/foaf/0.1/>
## PREFIX geosparql: <http://www.opengis.net/ont/geosparql#>
## PREFIX time: <http://www.w3.org/2006/time#>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX glvoc_P03: <http://schema.geolink.org/dev/voc/nvs/P03#>
PREFIX glvoc_L22: <http://schema.geolink.org/dev/voc/nvs/L22#>
PREFIX glview: <http://schema.geolink.org/dev/view#>

CONSTRUCT {
	?P03destURI a owl:Ontology ;
		rdfs:label ?P03graphdestLabel ;
		dcterms:date ?P03graphdestDate ;		
		rdfs:comment "This ontology captures SeaDataNet Agreed Parameter Groups from NERC vocabulary server"@en ;
		rdfs:seeAlso ?P03sourceURI .
	
	glview:Measurement a owl:Class .
	glview:MeasurementType a owl:Class .
	glview:hasMeasurementType a owl:ObjectProperty .
	
	?P03destTermURI a owl:Class, owl:NamedIndividual, glview:MeasurementType ; 
		rdfs:label ?P03destTermLabel ;
		rdfs:comment ?P03destTermDef ;
		rdfs:seeAlso ?P03destTermSeeAlso ;
		rdfs:subClassOf ?P03destSuperTermURI.
	[] 	a owl:Restriction; 
		owl:onProperty glview:hasMeasurementType; 
		owl:someValuesFrom [ a owl:Class ; owl:oneOf ( ?P03destTermURI ) ] ;
		rdfs:subClassOf ?P03destTermURI .	
		
}
WHERE {	
	VALUES ( ?P03sourceURI ?P03destURI ) { ( <http://vocab.nerc.ac.uk/collection/P03/current/> <http://schema.geolink.org/dev/voc/nvs/P03> ) }
	?P03sourceURI skos:member ?P03sourceTermURI ; 
		skos:prefLabel ?P03graphdestLabel ; 
		dcterms:date ?P03graphdestDate .	
	
	OPTIONAL { ?P03sourceTermURI skos:prefLabel ?P03sourceTermLabel . } 
	OPTIONAL { ?P03sourceTermURI skos:definition ?P03sourceTermDef . }
	OPTIONAL {	?P03sourceURI skos:member ?P03sourceSuperTermURI.
				?P03sourceTermURI skos:broader ?P03sourceSuperTermURI.	}
	BIND ( if (bound(?P03sourceTermLabel), ?P03sourceTermLabel, "") as ?P03destTermLabel )
	BIND ( if (bound(?P03sourceTermDef), ?P03sourceTermDef, "") as ?P03destTermDef )
	BIND ( ?P03sourceTermURI as ?P03destTermSeeAlso )
	BIND ( replace( replace( str(?P03sourceTermURI), str(?P03sourceURI), "", "i"), "/", "", "i") as ?P03term )
	BIND ( iri(concat(str(?P03destURI), "#", ?P03term )) as ?P03destTermURI )					
	BIND ( 
		if( !bound(?P03sourceSuperTermURI), 
			glview:Measurement, 
			iri(concat(str(?P03destURI), "#", replace(replace(str(?P03sourceSuperTermURI), str(?P03sourceURI), "", "i"), "/", "", "i") )) ) 
		as ?P03destSuperTermURI )
}
