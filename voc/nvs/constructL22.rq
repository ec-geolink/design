## This query is issued to NERC vocabulary server's SPARQL endpoint at http://vocab.nerc.ac.uk/sparql/
## Then, the result is saved into XML format as L22.owl

PREFIX owl: <http://www.w3.org/2002/07/owl#>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX xml: <http://www.w3.org/XML/1998/namespace>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX dcterms: <http://purl.org/dc/terms/>
## PREFIX foaf: <http://xmlns.com/foaf/0.1/>
## PREFIX geosparql: <http://www.opengis.net/ont/geosparql#>
## PREFIX time: <http://www.w3.org/2006/time#>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX glvoc_L22: <http://schema.geolink.org/dev/voc/nvs/L22#>
PREFIX glview: <http://schema.geolink.org/dev/view#>

CONSTRUCT {
  <http://schema.geolink.org/dev/voc/nvs/L22> a owl:Ontology ;
		rdfs:label ?ONTOL ;
		dcterms:date ?ONTODATE ;
		owl:versionInfo ?ONTOVERS ;
    rdfs:comment "This ontology captures instrument types from SeaVoX device catalogue as hosted in the NERC vocabulary server"@en ;
		rdfs:seeAlso <http://vocab.nerc.ac.uk/collection/L22/> .
  glview:Instrument a owl:Class .
  glview:InstrumentType a owl:Class .
  glview:hasInstrumentType a owl:ObjectProperty .  
  ?CLASS a owl:Class, owl:NamedIndividual, glview:InstrumentType ;
    rdfs:label ?L ;
    rdfs:comment ?DEF ;
		rdfs:seeAlso ?Y ;	 
		rdfs:subClassOf glview:Instrument ;
		owl:equivalentClass [ a owl:Restriction ; owl:onProperty glview:hasInstrumentType ; owl:hasValue ?CLASS ].
}
WHERE {
  <http://vocab.nerc.ac.uk/collection/L22/current/> skos:prefLabel ?ONTOL ; 
																										dcterms:date ?ONTODATE ;
																										owl:versionInfo ?ONTOVERS ;
																										skos:member ?Y .
  ?Y skos:prefLabel ?L ;
     skos:definition ?DEF .
  BIND (REPLACE(str(?Y),"vocab.nerc.ac.uk/collection/L22/current/","schema.geolink.org/dev/voc/nvs/L22#","i") AS ?Z)    
  BIND (IRI(SUBSTR(?Z,1,STRLEN(?Z)-1)) AS ?CLASS)
}