## This query is issued to NERC vocabulary server's SPARQL endpoint at http://vocab.nerc.ac.uk/sparql/
## Then, the result is saved into XML format as L22.owl
## Assumption: L05.owl is also generated

PREFIX owl: <http://www.w3.org/2002/07/owl#>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX xml: <http://www.w3.org/XML/1998/namespace>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX dcterms: <http://purl.org/dc/terms/>
## PREFIX foaf: <http://xmlns.com/foaf/0.1/>
## PREFIX geosparql: <http://www.opengis.net/ont/geosparql#>
## PREFIX time: <http://www.w3.org/2006/time#>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX glvoc_L22: <http://schema.geolink.org/dev/voc/nvs/L22#>
PREFIX glvoc_L05: <http://schema.geolink.org/dev/voc/nvs/L05#>
PREFIX glview: <http://schema.geolink.org/dev/view#>

CONSTRUCT {
	?L22destURI a owl:Ontology ;
		rdfs:label ?L22graphdestLabel ;
		dcterms:date ?L22graphdestDate ;
		owl:versionInfo ?L22graphdestVers ;
		rdfs:comment "This ontology captures SeaVoX device catalogue from NERC vocabulary server"@en ;		
		rdfs:seeAlso ?L22sourceURI .
	
	glview:Instrument a owl:Class .
	glview:InstrumentType a owl:Class .
	glview:hasInstrumentType a owl:ObjectProperty .

	?L05destSuperTermURI a owl:Class, owl:NamedIndividual .	
	?L22destTermURI a owl:Class, owl:NamedIndividual, glview:InstrumentType ; 
		rdfs:label ?L22destTermLabel ;
		rdfs:comment ?L22destTermDef ;
		rdfs:seeAlso ?L22destTermSeeAlso ;
		rdfs:subClassOf ?L22destSuperTermURI, ?L05destSuperTermURI.
	
	?L22destTermURI ?owlSameAsPred ?L05destSameTermURI ; ?owlEquivClassPred ?L05destSameTermURI .
	?L05destSameTermURIDecl a owl:Class, owl:NamedIndividual .
	[] 	a owl:Restriction; 
		owl:onProperty glview:hasInstrumentType; 
		owl:someValuesFrom [ a owl:Class ; owl:oneOf ( ?L22destTermURI ) ] ;
		rdfs:subClassOf ?L22destTermURI .	
		
}
WHERE {
	SELECT 	DISTINCT *
	WHERE {
		VALUES ( ?L22sourceURI ?L22destURI ) { ( <http://vocab.nerc.ac.uk/collection/L22/current/> <http://schema.geolink.org/dev/voc/nvs/L22> ) }		
		?L22sourceURI skos:member ?L22sourceTermURI ; 
			skos:prefLabel ?L22graphdestLabel ; 
			dcterms:date ?L22graphdestDate ; 
			owl:versionInfo ?L22graphdestVers .			
		?L22sourceTermURI skos:prefLabel ?L22sourceTermLabel;  skos:definition ?L22sourceTermDef .
		{	?L22sourceURI skos:member ?L22sourceSuperTermURI.
			?L22sourceTermURI skos:broader ?L22sourceSuperTermURI .		
		} UNION {		
			FILTER NOT EXISTS { ?L22sourceURI skos:member ?L22sourceSuperTermURI . ?L22sourceTermURI skos:broader ?L22sourceSuperTermURI .}				
		}
		VALUES ( ?L05sourceURI ?L05destURI ) { ( <http://vocab.nerc.ac.uk/collection/L05/current/> <http://schema.geolink.org/dev/voc/nvs/L05> ) }
		{	?L05sourceURI skos:member ?L05sourceSuperTermURI .
			?L22sourceTermURI skos:broader ?L05sourceSuperTermURI .			
		} UNION {
			FILTER NOT EXISTS { 
				?L05sourceURI skos:member ?L05sourceSuperTermURI . 
				?L22sourceTermURI skos:broader ?L05sourceSuperTermURI . 		
			}
		}
		{	?L05sourceURI skos:member ?L05sourceSameTermURI .
			?L22sourceTermURI owl:sameAs ?L05sourceSameTermURI .
		} UNION {
			FILTER NOT EXISTS { 
				?L05sourceURI skos:member ?L05sourceSameTermURI . 
				?L22sourceTermURI owl:sameAs ?L05sourceSameTermURI . 
			}
		}
		
		BIND ( ?L22sourceTermLabel as ?L22destTermLabel )
		BIND ( ?L22sourceTermDef as ?L22destTermDef )
		BIND ( ?L22sourceTermURI as ?L22destTermSeeAlso )
		BIND ( replace( replace( str(?L22sourceTermURI), str(?L22sourceURI), "", "i"), "/", "", "i") as ?L22term )
		BIND ( iri(concat(str(?L22destURI), "#", ?L22term )) as ?L22destTermURI )		
		
		BIND (	
			if(bound(?L22sourceSuperTermURI),
				iri(concat(str(?L22destURI), "#", replace(replace(str(?L22sourceSuperTermURI), str(?L22sourceURI), "", "i"), "/", "", "i") )),
				if(bound(?L05sourceSuperTermURI),
					iri(concat(str(?L05destURI), "#", replace(replace(str(?L05sourceSuperTermURI), str(?L05sourceURI), "", "i"), "/", "", "i") )),
					glview:Instrument
				)
			)				 
			as ?L22destSuperTermURI )
		
		BIND (
			if (bound(?L05sourceSuperTermURI),
				iri(concat(str(?L05destURI), "#", replace(replace(str(?L05sourceSuperTermURI), str(?L05sourceURI), "", "i"), "/", "", "i") )),
				if (bound(?L22sourceSuperTermURI),
					iri(concat(str(?L22destURI), "#", replace(replace(str(?L22sourceSuperTermURI), str(?L22sourceURI), "", "i"), "/", "", "i") )),
					glview:Instrument
				)
			)
			as ?L05destSuperTermURI)
		
		BIND (if (bound(?L05sourceSameTermURI), owl:sameAs, rdf:type) as ?owlSameAsPred )
		BIND (if (bound(?L05sourceSameTermURI), owl:equivalentClass, rdf:type) as ?owlEquivClassPred )		
		BIND (
			if (bound(?L05sourceSameTermURI), 
				iri(concat(str(?L05destURI), "#", replace(replace(str(?L05sourceSameTermURI), str(?L05sourceURI), "", "i"), "/", "", "i") )), 
				owl:Class
			)
			as ?L05destSameTermURI)
		BIND(if (bound(?L05sourceSameTermURI), ?L05destSameTermURI, ?L22destTermURI) as ?L05destSameTermURIDecl)
	}
}

