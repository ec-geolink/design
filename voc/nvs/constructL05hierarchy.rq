## This query is issued to NERC vocabulary server's SPARQL endpoint at http://vocab.nerc.ac.uk/sparql/
## Then, the result is saved into XML format as L05hierarchy.owl, giving the hierarchy in L05, which is imported by L05.owl

PREFIX owl: <http://www.w3.org/2002/07/owl#>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX xml: <http://www.w3.org/XML/1998/namespace>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX dcterms: <http://purl.org/dc/terms/>
PREFIX glvoc_L05: <http://schema.geolink.org/dev/voc/nvs/L05#>
PREFIX glview: <http://schema.geolink.org/dev/view#>
PREFIX L05: <http://vocab.nerc.ac.uk/collection/L05/current/>

CONSTRUCT {
	?VV rdfs:subClassOf ?WW .
	<http://schema.geolink.org/dev/voc/nvs/L05hierarchy> a owl:Ontology ;
		dcterms:date ?ONTODATE ;
		owl:versionInfo ?ONTOVERS ;
		rdfs:comment "This ontology captures SeaDataNet device category hierarchy from the NERC vocabulary server"@en ;
		rdfs:seeAlso <http://vocab.nerc.ac.uk/collection/L05/> .		  
  ?VV a owl:Class ;		rdfs:subClassOf ?WW .
	?WW a owl:Class .
		
}
WHERE {
	SELECT DISTINCT ?VV ?WW ?ONTODATE ?ONTOVERS
	WHERE {
		{ <http://vocab.nerc.ac.uk/collection/L05/current/> dcterms:date ?ONTODATE ;
																												owl:versionInfo ?ONTOVERS ;
																												skos:member ?V, ?W.	
			{ ?V skos:broader ?W . } UNION { ?W skos:narrower ?V . }
			BIND (REPLACE(str(?V),"vocab.nerc.ac.uk/collection/L05/current/","schema.geolink.org/dev/voc/nvs/L05#","i") AS ?VN)						
			BIND (IRI(SUBSTR(?VN,1,STRLEN(?VN)-1)) AS ?VV)
			BIND (REPLACE(str(?W),"vocab.nerc.ac.uk/collection/L05/current/","schema.geolink.org/dev/voc/nvs/L05#","i") AS ?WN)
			BIND (IRI(SUBSTR(?WN,1,STRLEN(?WN)-1)) AS ?WW)
		}
	} 
}