## This query is issued to NERC vocabulary server's SPARQL endpoint at http://vocab.nerc.ac.uk/sparql/
## Then, the result is saved into XML format as L06.owl, giving the base ontology

PREFIX owl: <http://www.w3.org/2002/07/owl#>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX xml: <http://www.w3.org/XML/1998/namespace>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX glvoc_l06: <http://schema.geolink.org/dev/voc/nvs/L06#>
PREFIX glview: <http://schema.geolink.org/dev/view#>
PREFIX l06: <http://vocab.nerc.ac.uk/collection/L06/current/>

CONSTRUCT {
	<http://schema.geolink.org/dev/voc/nvs/L06> a owl:Ontology ;
		rdfs:comment "This ontology captures SeaVoX platform categories (e.g., vessel, mooring, etc.) as hosted in the NERC vocabulary server"@en ;
		rdfs:seeAlso <http://vocab.nerc.ac.uk/collection/L06/> ;
		owl:imports <http://schema.geolink.org/dev/voc/nvs/L06hierarchy>.
  glview:Platform a owl:Class .
  glview:PlatformType a owl:Class .
  glview:hasPlatformType a owl:ObjectProperty .  
  ?VV a owl:Class, owl:NamedIndividual, glview:PlatformType  ;
		rdfs:label ?VL ;
		rdfs:comment ?VDEF ;
		rdfs:seeAlso ?V ;		
		rdfs:subClassOf glview:Platform ;
		owl:equivalentClass [ a owl:Restriction ; owl:onProperty glview:hasPlatformType ; owl:hasValue ?VV ]. 
}
WHERE {
	<http://vocab.nerc.ac.uk/collection/L06/current/> skos:member ?V.
	?V skos:prefLabel ?VL; skos:definition ?VDEF .
	BIND (REPLACE(str(?V),"vocab.nerc.ac.uk/collection/L06/current/","schema.geolink.org/dev/voc/nvs/L06#","i") AS ?VN)						
	BIND (IRI(SUBSTR(?VN,1,STRLEN(?VN)-1)) AS ?VV)
}

