## This query is issued to NERC vocabulary server's SPARQL endpoint at http://vocab.nerc.ac.uk/sparql/
## Then, the result is saved into XML format as L06.owl

PREFIX owl: <http://www.w3.org/2002/07/owl#>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX xml: <http://www.w3.org/XML/1998/namespace>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX dcterms: <http://purl.org/dc/terms/>
## PREFIX foaf: <http://xmlns.com/foaf/0.1/>
## PREFIX geosparql: <http://www.opengis.net/ont/geosparql#>
## PREFIX time: <http://www.w3.org/2006/time#>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX glvoc_L06: <http://schema.geolink.org/dev/voc/nvs/L06#>
PREFIX glview: <http://schema.geolink.org/dev/view#>

CONSTRUCT {
	?L06destURI a owl:Ontology ;
		rdfs:label ?L06graphdestLabel ;
		dcterms:date ?L06graphdestDate ;
		rdfs:comment "This ontology captures SeaVoX platform categories (e.g., vessel, mooring, etc.) as hosted in the NERC vocabulary server"@en ;
		rdfs:seeAlso ?L06sourceURI .
	
	glview:Platform a owl:Class .
	glview:PlatformType a owl:Class .
	glview:hasPlatformType a owl:ObjectProperty .
	
	?L06destTermURI a owl:Class, owl:NamedIndividual, glview:PlatformType ; 
		rdfs:label ?L06destTermLabel ;
		rdfs:seeAlso ?L06destTermSeeAlso ;
		rdfs:subClassOf ?L06destSuperTermURI.
	[] 	a owl:Restriction; 
		owl:onProperty glview:hasPlatformType; 
		owl:someValuesFrom [ a owl:Class ; owl:oneOf ( ?L06destTermURI ) ] ;
		rdfs:subClassOf ?L06destTermURI .	
		
}
WHERE {	
	VALUES ( ?L06sourceURI ?L06destURI ) { ( <http://vocab.nerc.ac.uk/collection/L06/current/> <http://schema.geolink.org/dev/voc/nvs/L06> ) }
	?L06sourceURI skos:member ?L06sourceTermURI ; 
		skos:prefLabel ?L06graphdestLabel ; 
		dcterms:date ?L06graphdestDate .	
	
	OPTIONAL { ?L06sourceTermURI skos:prefLabel ?L06sourceTermLabel. }
	OPTIONAL {	?L06sourceURI skos:member ?L06sourceSuperTermURI.
				?L06sourceTermURI skos:broader ?L06sourceSuperTermURI .	} 
	BIND ( if(bound(?L06sourceTermLabel), ?L06sourceTermLabel, "") as ?L06destTermLabel )
	BIND ( ?L06sourceTermURI as ?L06destTermSeeAlso )
	BIND ( replace( replace( str(?L06sourceTermURI), str(?L06sourceURI), "", "i"), "/", "", "i") as ?L06term )
	BIND ( iri(concat(str(?L06destURI), "#", ?L06term )) as ?L06destTermURI )					
	BIND ( 
		if( !bound(?L06sourceSuperTermURI), 
			glview:Platform, 
			iri(concat(str(?L06destURI), "#", replace(replace(str(?L06sourceSuperTermURI), str(?L06sourceURI), "", "i"), "/", "", "i") )) ) 
		as ?L06destSuperTermURI )

}
